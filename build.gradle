apply from: './version.gradle'

buildscript {
    ext {
        kotlinVersion = '1.3.31'
        springBootVersion = '2.2.7.RELEASE'
        protocVersion = '3.12.2'
        springDependencyVersion = '1.0.6.RELEASE'
        springPluginCoreVersion = '1.2.0.RELEASE'
        ktlintVersion = '0.36.0'
        junit5Version = '5.2.0'

        junit5LauncherVersion = '1.3.1'
        gitGradleVersion = '2.0.0'
        licenseGradleVersion = '1.5'
        reactorVersion = 'Dysprosium-SR4'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        // Spring Boot
        classpath "io.spring.gradle:dependency-management-plugin:${springDependencyVersion}"
        // Kotlin Language
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        // DI support
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        // JPA(Hibernate) no arg support
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
        // Git
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gitGradleVersion}")
        // License Report
        classpath("com.github.jk1:gradle-license-report:${licenseGradleVersion}")
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-spring'
apply plugin: 'kotlin-jpa'
apply plugin: 'idea'
apply plugin: 'io.spring.dependency-management'
apply plugin: "com.gorylenko.gradle-git-properties"
apply plugin: "com.github.jk1.dependency-license-report"

group = projectGroup
version = projectVersion

sourceCompatibility = 1.8

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    ktlint
}

allOpen {
    annotations("org.springframework.context.annotation.Configuration",
            "org.springframework.boot.autoconfigure.EnableAutoConfiguration")
}

dependencyManagement {
    imports {
        mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        mavenBom("io.projectreactor:reactor-bom:${reactorVersion}")
    }
}

dependencies {
    annotationProcessor("org.springframework.boot:spring-boot-autoconfigure-processor")
    compile('org.springframework.boot:spring-boot-autoconfigure')

    // Kafka
    implementation('org.springframework.kafka:spring-kafka')
    implementation("io.projectreactor.kafka:reactor-kafka")

    // Kotlin Support
    compile('org.jetbrains.kotlin:kotlin-stdlib')
    compile('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
    compile('org.jetbrains.kotlin:kotlin-reflect')
    implementation("io.projectreactor.kotlin:reactor-kotlin-extensions")
    implementation("javax.annotation:javax.annotation-api:1.3.2")

    // Jackson
    compile('com.fasterxml.jackson.core:jackson-databind')

    // Protobuf
    compile("com.google.protobuf:protobuf-java:${protocVersion}")

    // Lint
    ktlint("com.pinterest:ktlint:${ktlintVersion}")

    // Test
    testCompile('org.springframework.boot:spring-boot-starter-test')

    // JUnit5
    testCompile("org.junit.jupiter:junit-jupiter-api:${junit5Version}")
    testCompile("org.junit.jupiter:junit-jupiter-params:${junit5Version}")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:${junit5Version}")
    testRuntime("org.junit.vintage:junit-vintage-engine:${junit5Version}")
    testRuntime("org.junit.platform:junit-platform-launcher:${junit5LauncherVersion}")
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = projectGroup
            artifactId = projectName
            version = projectVersion

            from components.java
        }
    }
}

